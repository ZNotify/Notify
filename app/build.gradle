plugins {
    id "com.android.application"
    id "kotlin-android"
    id "org.jetbrains.kotlin.plugin.serialization" version "1.7.20"
    id "com.google.gms.google-services"
}

def gitCommitId = "git rev-parse --short HEAD".execute([], project.rootDir).text.trim()

def majorVersion = 0
def minorVersion = 1
def buildVersion = 1

def isDebug = gradle.startParameter.taskRequests.any {
    it.args.any { it.endsWith("Debug") }
}
def buildType = isDebug ? "Debug" : "Release"

// Get timestamp in seconds
def currentVersionCode = (new Date()).getTime() / 1000
def baseVersionName = "${majorVersion}.${minorVersion}.${buildVersion}"
def versionBase = "${baseVersionName}.${gitCommitId}.${buildType}"


static def releaseTime() {
    return new Date().format("yyyyMMdd", TimeZone.getTimeZone("UTC"))
}

android {
    defaultConfig {
        applicationId "top.learningman.push"
        minSdk 28
        targetSdk 33
        versionCode = currentVersionCode
        versionName = versionBase
    }

    signingConfigs {
        auto {
            def password
            def CI = System.getenv("CI")
            if (CI != null) {
                password = System.getenv("PASSWORD")
            } else {
                Properties properties = new Properties()
                properties.load(file("../local.properties").newDataInputStream())
                password = properties.getProperty("PASSWORD")
            }

            if (password == null || password.isEmpty()) {
                throw new Exception("Signing password not found.")
            }
            storeFile file("../release.jks")
            keyAlias "key"
            storePassword password
            keyPassword password
        }
    }
    compileSdk 33

    buildTypes {
        debug {
            signingConfig signingConfigs.auto
        }
        release {
            signingConfig signingConfigs.auto
            minifyEnabled true
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"

        freeCompilerArgs += [
                "-P",
                "plugin:androidx.compose.compiler.plugins.kotlin:suppressKotlinVersionCompatibilityCheck=1.7.20"
        ]
    }
    packagingOptions {
        resources {
            excludes += ["META-INF/atomicfu.kotlin_module"]
        }
    }
    viewBinding {
        enabled = true
    }

    namespace "top.learningman.push"

    buildFeatures {
        viewBinding true
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion '1.4.0-dev-k1.7.20-RC-a143c065804'
    }
    dependenciesInfo {
        includeInApk false
        includeInBundle false
    }
}

dependencies {
    implementation "androidx.core:core-ktx:1.9.0"
    implementation 'androidx.core:core-splashscreen:1.0.0'
    implementation "androidx.appcompat:appcompat:1.5.1"

    implementation 'androidx.activity:activity-compose:1.6.0'
    implementation "androidx.compose.material3:material3:1.0.0-beta03"
    implementation "androidx.compose.material3:material3-window-size-class:1.0.0-beta03"
    implementation 'androidx.compose.animation:animation:1.2.1'
    implementation 'androidx.compose.ui:ui-tooling:1.2.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.5.1'
    implementation "com.google.android.material:compose-theme-adapter:1.1.19"

    implementation "com.google.android.material:material:1.6.1"

    implementation platform('com.google.firebase:firebase-bom:30.5.0')
    implementation "com.google.firebase:firebase-analytics-ktx"
    implementation "com.google.firebase:firebase-messaging-ktx"

    implementation "androidx.constraintlayout:constraintlayout:2.1.4"
    implementation "androidx.preference:preference-ktx:1.2.0"
    implementation 'androidx.navigation:navigation-fragment-ktx:2.5.2'
    implementation 'androidx.navigation:navigation-ui-ktx:2.5.2'

    def work_version = "2.7.1"
    implementation "androidx.work:work-runtime:$work_version"
    implementation "androidx.work:work-runtime-ktx:$work_version"

    def lifecycle_version = "2.5.1"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-service:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"

    implementation "androidx.fragment:fragment-ktx:1.5.3"
    implementation "androidx.activity:activity-ktx:1.6.0"

    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.4.0"

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4"

    implementation "com.github.code-mc:material-icon-lib:1.1.5"

    def ktor_version = '2.1.2'
    implementation "io.ktor:ktor-client-core:$ktor_version"
    implementation "io.ktor:ktor-client-websockets:$ktor_version"
    implementation "io.ktor:ktor-client-okhttp:$ktor_version"

    implementation "dev.zxilly:notify-sdk:2.0.1"

    def markwonVersion = "4.6.2"
    implementation "io.noties.markwon:core:${markwonVersion}"
    implementation "io.noties.markwon:ext-tables:${markwonVersion}"
    implementation "io.noties.markwon:html:${markwonVersion}"
    implementation "io.noties.markwon:image:${markwonVersion}"

    def appCenterSdkVersion = "4.4.5"
    implementation "com.microsoft.appcenter:appcenter-analytics:${appCenterSdkVersion}"
    implementation "com.microsoft.appcenter:appcenter-crashes:${appCenterSdkVersion}"

    implementation 'com.github.Zxilly:SetupWizardLib:master-SNAPSHOT'
    implementation 'com.github.XomaDev:MIUI-autostart:master-SNAPSHOT'

    implementation 'com.github.Zxilly:upgrader:master-9e64933-1'
}