plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
    id 'kotlin-kapt'
}

def gitCommitId = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()

def majorVersion = 0
def minorVersion = 0
def buildVersion = 1
def currentVersionCode = (majorVersion + 1) * 200 + minorVersion * 20 + buildVersion
def baseVersionName = "${majorVersion}.${minorVersion}.${buildVersion}"
def versionBase = "${baseVersionName}.${releaseTime()}.${gitCommitId}"


static def releaseTime() {
    return new Date().format("yyyyMMdd", TimeZone.getTimeZone("UTC"))
}

android {
    signingConfigs {
        debug {
            storeFile file('key.jks')
            storePassword 'password'
            keyAlias 'notify'
            keyPassword 'password'
        }
        release {
            storeFile file('key.jks')
            storePassword 'password'
            keyAlias 'notify'
            keyPassword 'password'
        }
    }
    compileSdk 30

    defaultConfig {
        applicationId "top.learningman.mipush"
        minSdk 26
        targetSdk 30
        versionCode = currentVersionCode
        versionName = versionBase + "-SNAPSHOT"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        signingConfig signingConfigs.release
    }

    buildTypes {
        debug {
            buildConfigField "String", "APIURL", "\"https://push.learningman.top\""
        }
        release {
            buildConfigField "String", "APIURL", "\"https://push.learningman.top\""
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    packagingOptions {
        exclude 'META-INF/atomicfu.kotlin_module'
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'

    implementation 'com.google.android.material:material:1.4.0'

    implementation 'androidx.constraintlayout:constraintlayout:2.1.0'
    implementation 'androidx.preference:preference-ktx:1.1.1'

    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.3.1"
    implementation "androidx.lifecycle:lifecycle-common-java8:2.3.1"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.5.30"
    api "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.1"
    api "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.1"

    implementation 'androidx.room:room-runtime:2.3.0'
    implementation 'androidx.room:room-ktx:2.3.0'
    kapt 'androidx.room:room-compiler:2.3.0'
    androidTestImplementation "androidx.room:room-testing:2.3.0"

    implementation 'net.steamcrafted:materialiconlib:1.1.5'
    implementation 'com.squareup.okhttp3:okhttp:4.9.1'
    implementation files('libs/MiPush_SDK_Client_4_8_0.jar')

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}