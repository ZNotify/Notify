plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-android-extensions' // TODO: Migrate to view binding
    id 'com.google.gms.google-services'
}

def gitCommitId = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()

def majorVersion = 0
def minorVersion = 0
def buildVersion = 1
def currentVersionCode = (majorVersion + 1) * 200 + minorVersion * 20 + buildVersion
def baseVersionName = "${majorVersion}.${minorVersion}.${buildVersion}"
def versionBase = "${baseVersionName}.${releaseTime()}.${gitCommitId}"


static def releaseTime() {
    return new Date().format("yyyyMMdd", TimeZone.getTimeZone("UTC"))
}

android {
    signingConfigs {
        debug {
            storeFile file('key.jks')
            storePassword 'password'
            keyAlias 'notify'
            keyPassword 'password'
        }
        release {
            storeFile file('key.jks')
            storePassword 'password'
            keyAlias 'notify'
            keyPassword 'password'
        }
    }
    compileSdk 32

    defaultConfig {
        applicationId "top.learningman.push"
        minSdk 26
        targetSdk 32
        versionCode = currentVersionCode
        versionName = versionBase + "-SNAPSHOT"

        signingConfig signingConfigs.release
    }

    buildTypes {
        debug {
            buildConfigField "String", "APIURL", "\"http://192.168.0.105:14444\""
        }
        release {
            buildConfigField "String", "APIURL", "\"https://push.learningman.top\""
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/atomicfu.kotlin_module']
        }
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'

    implementation 'com.google.android.material:material:1.6.0'

    implementation platform('com.google.firebase:firebase-bom:29.2.1')
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-messaging-ktx'

    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'androidx.preference:preference-ktx:1.2.0'

    implementation 'androidx.lifecycle:lifecycle-common-java8:2.4.1'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.4.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.1'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.4.1'

    implementation 'androidx.fragment:fragment-ktx:1.4.1'
    implementation 'androidx.activity:activity-ktx:1.4.0'

    api "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.1"
    api "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.1"

    implementation 'net.steamcrafted:materialiconlib:1.1.5' // TODO: deprecate this dependency
    implementation 'com.squareup.okhttp3:okhttp:4.9.3'
    implementation 'com.google.code.gson:gson:2.9.0'

    def markwonVersion = '4.6.2'
    implementation "io.noties.markwon:core:${markwonVersion}"
    implementation "io.noties.markwon:ext-tables:${markwonVersion}"
    implementation "io.noties.markwon:html:${markwonVersion}"
    implementation "io.noties.markwon:image:${markwonVersion}"

    implementation files('libs/MiPush_SDK_Client_5_0_5.aar')

}