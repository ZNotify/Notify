plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'com.google.gms.google-services'
}

def gitCommitId = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()

def majorVersion = 0
def minorVersion = 0
def buildVersion = 1
// Get timestamp in seconds
def currentVersionCode = (new Date()).getTime() / 1000
def baseVersionName = "${majorVersion}.${minorVersion}.${buildVersion}"
def versionBase = "${baseVersionName}.${releaseTime()}.${gitCommitId}"


static def releaseTime() {
    return new Date().format("yyyyMMdd", TimeZone.getTimeZone("UTC"))
}

android {
    defaultConfig {
        applicationId "top.learningman.push"
        minSdk 26
        targetSdk 32
        versionCode = currentVersionCode
        versionName = versionBase + "-SNAPSHOT"
    }

    signingConfigs {
        release {
            def password
            def CI = System.getenv("CI")
            if (CI != null) {
                password = System.getenv("PASSWORD")
            } else {
                Properties properties = new Properties()
                properties.load(file('../local.properties').newDataInputStream())
                password = properties.getProperty('PASSWORD')
            }

            if (password == null || password.isEmpty()) {
                throw new Exception("Signing password not found.")
            }
            storeFile file('../release.jks')
            keyAlias 'key'
            storePassword password
            keyPassword password
        }
    }
    compileSdk 32

    buildTypes {
        debug {
            signingConfig signingConfigs.release
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/atomicfu.kotlin_module']
        }
    }
    viewBinding {
        enabled = true
    }
    namespace 'top.learningman.push'
}

dependencies {
    implementation 'androidx.core:core-ktx:1.8.0'
    implementation 'androidx.appcompat:appcompat:1.4.2'

    implementation 'com.google.android.material:material:1.6.1'

    implementation platform('com.google.firebase:firebase-bom:29.2.1')
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-messaging-ktx'

    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.preference:preference-ktx:1.2.0'

    implementation 'androidx.lifecycle:lifecycle-common-java8:2.4.1'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.4.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.1'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.4.1'

    implementation 'androidx.fragment:fragment-ktx:1.4.1'
    implementation 'androidx.activity:activity-ktx:1.4.0'

    api "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.2"
    api "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.2"

    implementation 'net.steamcrafted:materialiconlib:1.1.5' // TODO: deprecate this dependency
    implementation 'com.squareup.okhttp3:okhttp:4.9.3'
    implementation 'com.google.code.gson:gson:2.9.0'

    implementation 'dev.zxilly:notify-sdk:1.1.0'

    def markwonVersion = '4.6.2'
    implementation "io.noties.markwon:core:${markwonVersion}"
    implementation "io.noties.markwon:ext-tables:${markwonVersion}"
    implementation "io.noties.markwon:html:${markwonVersion}"
    implementation "io.noties.markwon:image:${markwonVersion}"

    def appCenterSdkVersion = '4.4.3'
    implementation "com.microsoft.appcenter:appcenter-analytics:${appCenterSdkVersion}"
    implementation "com.microsoft.appcenter:appcenter-crashes:${appCenterSdkVersion}"
    implementation "com.microsoft.appcenter:appcenter-distribute:${appCenterSdkVersion}"

    implementation files('libs/MiPush_SDK_Client_5_0_5.aar')
}